## options

# compacked complete list display
#
[[ $OSTYPE = "darwin10.0" || $OSTYPE = "darwin11.0" || $OSTYPE = "darwin12.0" || $OSTYPE = "darwin13.0" ]] && [[ $MACOSX = '' ]] && MACOSX='yes'
[[ $OSTYPE = "cygwin" ]] && [[ $CYGWIN = '' ]] && CYGWIN='yes'

# completion list item width equal to 'ls' item width
[ $MACOSX ] && setopt no_list_packed || setopt list_packed

## paths
if [ $MACOSX ]; then
  export PATH=$PATH:/usr/local/bin
fi

export PATH=$HOME/bin:$PATH

export LC_ALL=en_US.UTF-8


##aliases
if [ $MACOSX ]; then
  alias ls='ls -G'
else
  alias ls='ls --color'
fi
alias ll='ls -l'
alias lh='ls -lh'
alias la='ls -a'

alias g='git'
alias gs='git status'
alias s='screen'

# global alias - works not only as the first word of command
alias -g ...='../..'
alias -g ....='../../..'
alias -g .....='../../../..'

alias cd..='cd ..'
alias cd-='cd -'

alias -g G='| grep'
alias -g L='| less'

# without this, git log, git diff, etc. don't use alternate screen
#alias git='LESS=FRS git'


if [ $MACOSX ]; then
  alias pbcopy="__CF_USER_TEXT_ENCODING=0x$(printf %x $(id -u)):0x08000100:14 pbcopy"
  alias ql='qlmanage -p "$@" >& /dev/null'
  alias diff='colordiff'

  source ~/.phpbrew/bashrc

  eval "$(rbenv init -)"
  # path_helper issue https://github.com/sstephenson/rbenv/issues/369
  export PATH=$(echo :$PATH | perl -pe 's@^(.*):([^:]+rbenv/shims)(.*)$@\2\1\3@')

  eval "$(pyenv init -)"
  # path_helper issue https://github.com/sstephenson/rbenv/issues/369
  export PATH=$(echo :$PATH | perl -pe 's@^(.*):([^:]+pyenv/shims)(.*)$@\2\1\3@')

  export PATH="/usr/local/heroku/bin:$PATH"
fi

# http://nion.modprobe.de/blog/archives/569-colored-manpages.html
alias man='TERMINFO=~/.terminfo/ LESS=C TERM=mostlike PAGER=less man'


# mkcd = mkdir + cd
function mkcd(){
  eval dir=\$\{$#\}
  mkdir $@ && cd $dir
}


## other

#set color of ls -G
#see also "man ls"
#http://webtech-walker.com/archive/2008/12/15101251.html
export LSCOLORS=gxfxcxdxbxegedabagacad
export LS_COLORS='di=01;36:ln=01;35:ex=01;31'
zstyle ':completion:*' list-colors 'di=36' 'ln=35' 'ex=31'
#color autocomplete list as the ls color
#zstyle ':completion:*' list-colors ${(s.:.)LSCOLORS}

# case-insensitive (all),partial-word and then substring completion
# zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
# http://wiki.fdiary.net/zsh/?FAQ%40zsh%A5%B9%A5%EC
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'


#set window/tab title 
#http://www.macosxhints.com/article.php?story=20060502160527780
precmd_set_tab_title() {
  print -Pn "\e]0;%~/\a"
}
precmd_functions=($precmd_functions precmd_set_tab_title)

# ls after cd
function chpwd_ls() {
  ls
}
chpwd_functions=($chpwd_functions chpwd_ls)

#when using GNU screen, set window title as directory & command
#http://shunirr.org/archives/104
if [ $TERM = "screen" ]; then
  precmd_set_screen_title () {
    screen -X title $(basename $(pwd))
  }
  precmd_functions=($precmd_functions precmd_set_screen_title)

  #preexec_set_screen_title () {
     #screen -X title "$1"
  #}
  #preexec_functions=($preexec_functions preexec_set_screen_title)
fi


## can't move cursor in 'vi' under 'screen'
##http://www.takulab.com/mac/mac-vim-configure/
if [ $TERM = "screen" ]; then
  if [ $MAC ]; then
    export TERM=dtterm
  fi
fi


# parens ()
function insert_parens() {
  LBUFFER+="()"
  zle backward-char
}
zle -N insert-parens insert_parens
bindkey "(" insert-parens

function close_parens() {
  if [[ $#RBUFFER != "" ]] && [[ $RBUFFER[1] = ")" ]]; then
    zle forward-char
  else
    zle self-insert
  fi
}
zle -N close-parens close_parens
bindkey ")" close-parens

# braces {}
function insert_braces() {
  LBUFFER+="{}"
  zle backward-char
}
zle -N insert-braces insert_braces
bindkey "{" insert-braces

function close_braces() {
  if [[ $RBUFFER[1] = "}" ]]; then
    zle forward-char
  else
    zle self-insert
  fi
}
zle -N close-braces close_braces
bindkey "}" close-braces

# brackets []
function insert_brackets() {
  LBUFFER+="[]"
  zle backward-char
}
zle -N insert-brackets insert_brackets
bindkey "[" insert-brackets

function close_brackets() {
  if [[ $RBUFFER[1] = ']' ]]; then
    zle forward-char
  else
    LBUFFER+="]"
  fi
}
zle -N close-brackets close_brackets
bindkey "]" close-brackets

# quotes ''
function insert_quotes() {
  if [[ $RBUFFER[1] = "'" ]]; then
    zle forward-char
  else
    if [[ "${LBUFFER%[[:alpha:]]}" == "$LBUFFER" ]]; then # last of LBUFFER is not an alphabet
      LBUFFER+="''"
      zle backward-char
    else
      zle self-insert
    fi
  fi
}
zle -N insert-quotes insert_quotes
bindkey "'" insert-quotes

# double quotes ""
function insert_double_quotes() {
  if [[ $RBUFFER[1] = '"' ]]; then
    zle forward-char
  else
    LBUFFER+='""'
    zle backward-char
  fi
}
zle -N insert-double-quotes insert_double_quotes
bindkey '"' insert-double-quotes


if [ -f "$HOME/.zsh/auto-fu.zsh" ]; then
  # auto-fu.zsh http://d.hatena.ne.jp/hchbaw/20100526/1274886125
  . $HOME/.zsh/auto-fu.zsh
  zle-line-init () {auto-fu-init;}
  zle -N zle-line-init
fi


# go backward in completion list
bindkey '^[[Z' reverse-menu-complete

# run in cmd.exe (on cygwin), with a .bat file
# http://stackoverflow.com/questions/366928/invoking-cl-exe-msvc-compiler-in-cygwin-shell
if [ $CYGWIN ]; then
  function run_with_bat()
  {
    batfile=$1; shift
    tmpfile="$TMP/tmp$$.bat"
    echo "@echo off" > $tmpfile
    echo "call \"%$batfile%vsvars32.bat\" >NUL:" >> $tmpfile
    echo "bash -c \"%*\"" >> $tmpfile
    cmd /c `cygpath -m "$tmpfile"` "$@"
    returnstatus=$?
    rm -f $tmpfile
    return $returnstatus
  }

  function run_vs10()
  {
    run_with_bat VS100COMNTOOLS "$@"
  }

  alias cl="run_vs10 cl"
  alias nmake="run_vs10 nmake"
  alias rc="run_vs10 rc"
fi


# use glob in hisotry search
# http://subtech.g.hatena.ne.jp/secondlife/20110222/1298354852
bindkey '^R' history-incremental-pattern-search-backward
bindkey '^S' history-incremental-pattern-search-forward

# Ctrl-rの履歴検索の逆順を使うために、端末のほうのCtrl-sを無効にする
# http://www.jp-z.jp/changelog/2006-09-28-2.html
stty stop undef


# http://d.hatena.ne.jp/NeoCat/20120822/1345657535

#右プロンプト
# %F{～}は色指定、%fでリセット
# %nはログインユーザ名、%~はカレントディレクトリ
# "%(?..%F{red}-%?-)" は終了コードが0以外なら赤色で表示
# "%1(v|%F{yellow}%1v%F{green} |)" の部分がVCS情報 (psvarの長さが1以上なら黄色で表示)
RPROMPT="%(?..%F{red}-%?-)%F{green}[%1(v|%F{yellow}%1v%F{green} |)%n:%~]%f"

# コマンドの実行直後に右プロンプトを消す
# http://www.machu.jp/diary/20130114.html#p01
setopt transient_rprompt

#gitブランチ名表示
autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git:*' formats '%c%u%b'
zstyle ':vcs_info:git:*' actionformats '%c%u%b|%a'

#git情報更新
update_vcs_info() {
  psvar=()
  LANG=en_US.UTF-8 vcs_info
  [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
}

local _cmd=''
preexec_save_last_command() {
  _cmd="$1"
}
preexec_functions=($preexec_functions preexec_save_last_command)

#カレントディレクトリ変更時/git関連コマンド実行時に情報更新
chpwd_show_git_info() {
  update_vcs_info
}
chpwd_functions=($chpwd_functions chpwd_show_git_info)

precmd_show_git_info() {
  _r=$?
  case "${_cmd}" in
    g*|git*|stg*) update_vcs_info ;;
  esac
  return $_r
}
precmd_functions=($precmd_functions precmd_show_git_info)

# 補完候補をカーソルキーで選択できるようにする
# http://kaworu.jpn.org/kaworu/2012-04-17-1.php
zstyle ':completion:*:default' menu select=4


# SSH ForwardAgentの設定で接続の切れたscreenにattachすると環境変数が変わってなくてFowardされないため
# http://blog.s21g.com/articles/97
if [ "$TERM" = "screen" ]; then
  alias fixsshenv='eval `cat ~/.ssh/fix_ssh_env`'
  #alias ssh='fixsshenv; ssh'
  alias g='fixsshenv; git'
else
  export | grep '^SSH_' > ~/.ssh/fix_ssh_env
fi


show_buffer_stack() {
  POSTDISPLAY="
stack: $LBUFFER"
  zle push-line
}
zle -N show_buffer_stack
bindkey "^[q" show_buffer_stack

